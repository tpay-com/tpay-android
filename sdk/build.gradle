import com.vanniktech.maven.publish.SonatypeHost

plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.dokka'
    id 'signing'
    id 'kotlin-parcelize'
    id 'com.vanniktech.maven.publish'
}

def _versionMajor = 1
def _versionMinor = 2
def _versionPatch = 4
def _versionName = "${_versionMajor}.${_versionMinor}.${_versionPatch}"
def _archiveName = "com.tpay.sdk-$_versionName"

android {
    namespace = "com.tpay.sdk"
    compileSdk rootProject.compileSdk

    defaultConfig {
        minSdk rootProject.minSdk
        targetSdk rootProject.targetSdk
        setProperty("archivesBaseName", _archiveName)

        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }
    buildFeatures {
        viewBinding true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    // Android
    implementation "androidx.appcompat:appcompat:$appCompat"
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayout"
    implementation "androidx.recyclerview:recyclerview:$recyclerView"
    implementation "androidx.fragment:fragment-ktx:$fragmentKtx"

    // Google Pay
    implementation "com.google.android.gms:play-services-wallet:$googlePay"

    //Inject
    implementation 'javax.inject:javax.inject:1'

    // Material
    implementation "com.google.android.material:material:$material"

    testImplementation "junit:junit:$jUnit"
    androidTestImplementation "androidx.test.ext:junit:$androidxJUnit"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso"
}

dokkaHtml.configure {
    dokkaSourceSets {
        named("main") {
            noAndroidSdkLink.set(false)
            includeNonPublic.set(false)
        }
    }
}

task generateDocumentation(type: Jar) {
    archiveClassifier = "javadoc"
    dependsOn(dokkaJavadoc)
    from(dokkaJavadoc.outputDirectory)
}

task sourcesJar(type: Jar) {
    archiveClassifier = "sources"
    from 'src/main/kotlin/com/tpay/sdk/api'
}

def isSigningDisabled = project.hasProperty("skipSigning") || (project.hasProperty("mavenPublishSigningEnabled") && !project.mavenPublishSigningEnabled.toBoolean())

gradle.taskGraph.whenReady {
    if (isSigningDisabled) {
        tasks.withType(Sign).configureEach {
            enabled = false
        }
    }
}
afterEvaluate {
    signing {
        useInMemoryPgpKeys(
                System.getenv('SIGNING_KEY'),
                System.getenv('SIGNING_KEY_PASSWORD')
        )

        sign publishing.publications
    }
    publishing {
        repositories {
            maven {
                name 'GitlabRegistry'
                url System.getenv('GITLAB_REGISTRY_URL')
                credentials(HttpHeaderCredentials) {
                    name = "Job-Token"
                    value = System.getenv("CI_JOB_TOKEN")
                }
                authentication {
                    header(HttpHeaderAuthentication)
                }
            }

            maven {
                name 'Local'
                url "$buildDir/tpayMaven/"
            }
        }

        mavenPublishing {
            // Set to true for automatic release to Central Portal
            publishToMavenCentral(SonatypeHost.CENTRAL_PORTAL, false)
            signAllPublications()

            coordinates("com.tpay", "sdk", "${_versionName}")

            // the following is optional

            pom {
                name = "Tpay SDK"
                description = "Tpay - online payment operator for Android"
                url = "https://tpay.com/"
                licenses {
                    license {
                        name = "MIT License"
                        url = "http://www.opensource.org/licenses/mit-license.php"
                    }
                }
                scm {
                    url = "https://github.com/tpay-com/tpay-android"
                    connection = "scm:git:git://github.com/tpay-com/tpay-android.git"
                    developerConnection = "scm:git:git://github.com/tpay-com/tpay-android.git"
                }
                developers {
                    developer {
                        id = "com.tpay.devs"
                        name = "Tpay Auto Commit"
                    }
                }
            }
        }
    }
}
